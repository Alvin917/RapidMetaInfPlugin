import com.wangjiegulu.plg.rapidmetainf.RapidMetaInfPlugin
//
apply plugin: 'com.android.library'

//com.wangjie.myplugin为resources/META-INF/gradle-plugins
//下的properties文件名称
//apply plugin: 'com.github.wangjiegulu.plg.rapidmetainf'
apply plugin: RapidMetaInfPlugin

rapidmetainf {
    metaInfName 'depmodule.properties'
    metaInfProperties "appName=$appName",
            "appVersion=$appVersion",
            "propertyFromCommand=${getParameter('commandKey')}" // ./gradlew :depmodule:clean :depmodule:build -PcommandKey=commandValue
}

def getParameter(String key) {
    // -D
    String value = System.getProperty(key)
    if (null != value && value.length() > 0) {
        return value
    }
    // -P
    if (hasProperty(key)) {
        return getProperty(key)
    }
    return null
}

android {
    compileSdkVersion global_compileSdkVersion

    defaultConfig {
        minSdkVersion global_minSdkVersion
        targetSdkVersion global_targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.android.support:appcompat-v7:$global_supportVersion"
    testImplementation "junit:junit:$global_junitVersion"
    androidTestImplementation "com.android.support.test:runner:$global_runnerVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$global_espressoVersion"
}

